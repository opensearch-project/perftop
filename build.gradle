/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 *
 * Modifications Copyright OpenSearch Contributors. See
 * GitHub history for details.
 */

/*
* Copyright 2019 Amazon.com, Inc. or its affiliates. All Rights Reserved.
*
* Licensed under the Apache License, Version 2.0 (the "License").
* You may not use this file except in compliance with the License.
* A copy of the License is located at
*
*     http://www.apache.org/licenses/LICENSE-2.0
*
* or in the "license" file accompanying this file. This file is distributed
* on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either
* express or implied. See the License for the specific language governing
* permissions and limitations under the License.
*/

buildscript {

    ext {
        opensearch_version = System.getProperty("opensearch.version", "1.0.0")
    }
    // This isn't applying from repositories.gradle so repeating it here
    repositories {
        mavenCentral()
        mavenLocal()
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }

    dependencies {
        classpath "org.opensearch.gradle:build-tools:${opensearch_version}-SNAPSHOT"
    }
}

plugins {
    id "java"
    id "com.moowork.node" version "1.2.0"
}

node {
    version = "10.15.0"
    download = false
}

ext {
    opendistroVersion = '1.13.0'
    isSnapshot = "true" == System.getProperty("build.snapshot", "true")
    isLinux = "true" == System.getProperty("build.linux", "false")
    isMacOS = "true" == System.getProperty("build.macos", "false")
}

group = "com.amazon.opendistroforelasticsearch"
version = "${opendistroVersion}.0"
if (isLinux) {
    version += "-linux-x64"
}
if (isMacOS) {
    version += "-macos-x64"
}
if (isSnapshot) {
    version += "-SNAPSHOT"
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url "https://plugins.gradle.org/m2/" }
    jcenter()
}

apply plugin: 'opensearch.opensearchplugin'

ext {
    projectSubstitutions = [:]
    licenseFile = rootProject.file('LICENSE.txt')
    noticeFile = rootProject.file('NOTICE.txt')
}
test {
    enabled = false
}
licenseHeaders.enabled = false
validateNebulaPom.enabled = false

task npmRunBuild(type: NpmTask, dependsOn: 'npmInstall') {
    if (isLinux) {
        args = ['run', 'build-linux']
    }
    if (isMacOS) {
        args = ['run', 'build-macos']
    }
}

task npmRunClean(type: NpmTask) {
    args = ['run', 'clean']
}

compileJava {
    dependsOn npmRunBuild
}

clean {
    dependsOn npmRunClean
}

// We are excluding plugin descriptor for PerfTop
// The info here could be random
opensearchplugin {
    name 'opendistro-perf-top'
    description 'PerfTop'
    classname 'com.amazon.opendistro.opensearch.performanceanalyzer.PerformanceAnalyzerPlugin'
}


bundlePlugin {
    exclude "opendistro-perf-top*.jar"
    exclude "plugin-descriptor.properties"
    if (isLinux) {
        from ("build/") {
            include "opendistro-perf-top-linux"
        }
    }
    if (isMacOS) {
        from ("build/") {
            include "opendistro-perf-top-macos"
        }
    }
    from ("dashboards/") {
        into "dashboards/"
        include "*.json"
    }
}

// The testingConventions task now mandates atleast one of the integ test tasks to be performed.
// Since perftop is not really a plugin on its own, it fails the plugin install step during the integ tests. So,
// skip running testingConventions task.
gradle.startParameter.excludedTaskNames += ["testingConventions"]
